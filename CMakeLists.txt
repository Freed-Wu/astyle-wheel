cmake_minimum_required(VERSION 3.10)
# cmake-format: off
set(VERSION 3.4.13 CACHE STRING "version of astyle")
# cmake-format: on

include(FetchContent)
FetchContent_Declare(astyle URL "https://gitlab.com/saalen/astyle/-/archive/${VERSION}/astyle-${VERSION}.tar.gz")
FetchContent_MakeAvailable(astyle)

find_package(Python COMPONENTS Development.Module)
if(Python_FOUND)
  set(astyle_dir "${astyle_SOURCE_DIR}/AStyle/src")
  aux_source_directory("${astyle_dir}" sources)
  add_library(__init__ SHARED "src/astyle/__init__.c" "src/astyle/wrap.cc" "${sources}")
  target_compile_definitions(__init__ PRIVATE ASTYLE_LIB)
  if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
    set_target_properties(__init__ PROPERTIES PREFIX "" SUFFIX ".${Python_SOABI}.pyd")
  else()
    set_target_properties(__init__ PROPERTIES PREFIX "" SUFFIX ".${Python_SOABI}${CMAKE_SHARED_MODULE_SUFFIX}")
  endif()
  target_include_directories(__init__ PRIVATE "${Python_INCLUDE_DIRS}" "${astyle_dir}")
  if(NOT SKBUILD)
    set(SKBUILD_PLATLIB_DIR "${CMAKE_CURRENT_SOURCE_DIR}/src")
  endif()
  install(TARGETS __init__ DESTINATION "${SKBUILD_PLATLIB_DIR}/astyle")
endif()
